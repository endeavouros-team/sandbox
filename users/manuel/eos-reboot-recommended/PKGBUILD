# Maintainer: EndeavourOS-Team <info@endeavouros.com>

# Moved to eos-reboot-recommended from eos-bash-shared.

pkgname=eos-reboot-recommended
pkgdesc="Notify if a reboot is recommended after system upgrade."
pkgver=25.11.1
pkgrel=1
arch=('any')
license=('GPL')
depends=(
    eos-bash-shared    # notification help, detect the running kernel, detect if in chroot
    libnotify          # notifications
)

url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/refs/heads/master/$pkgname"

source=(
  $_url/eos-reboot-required2
  $_url/eos-reboot-required3
  # $_url/eos-reboot-required.conf
  $_url/eos-reboot-required.hook
  $_url/eos-reboot-required.timer
  $_url/eos-reboot-required.service
)

sha512sums=('afa764e6ffc507b4518abbf76b8a61af96c3ab87b6c8ab81b8a867d8303b43c16df0fdec8331e403bd8411c1b1a5246df7294d2c356d2a68abe7b3a9820a4a31'
            '5f6dc4b03cdfaab833d8bafd5a1b2c51dbf1829e44156884e58e7757180235fb3e514308337a9f36343642f78de0ed0188c182e09779bacf4af80f7abd152e56'
            '195cc83df58e1f65b43f39202fe60aecf348e843c9c1b414897dbf6577ee7ffc67b1d1ba5ca0325e501cffebd304b6eca90f1c8fd4af4dd75b94fbc7834f59f4'
            '28af923b4f7fbfdf1b28a5f835944dc75f2f0e69a38fab565d284686938049fa4a19fed397eeea02eab6cab1894a749db045e810080dddfda394e8e874e0023a'
            '00557e96e9b5155424e83ae87fb242dc23968d79e63ea8831bddaf697ef5267351bc7fa2a7343b6a5c9aadaf170759e62ee1210f3dce839e395c51b00eb13d2f')

package() {
  local file src

  for src in "${source[@]}" ; do
      file="${src##*/}"
      case "$file" in
          *.conf)                                   install -Dm644 "$file"   "$pkgdir/etc/$file" ;;
          *.hook)                                   install -Dm644 "$file"   "$pkgdir/usr/share/libalpm/hooks/$file" ;;
          *.timer | *.service)                      install -Dm644 "$file"   "$pkgdir/usr/lib/systemd/system/$file" ;;
          *)                                        install -Dm755 "$file"   "$pkgdir/usr/bin/$file" ;;
      esac
  done
}
